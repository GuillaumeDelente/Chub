apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "io.chub.android"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 2
        versionName "1.0-beta2"
    }

    signingConfigs {
        release {
            //Release info set in afterEvaluate at the bottom
            storeFile file("plop")
            keyAlias "plop"
            storePassword "plop"
            keyPassword "plop"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    mavenLocal()
    maven {
        //For OkHttp interceptors
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava-mock:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.okhttp:logging-interceptor:2.6.0-SNAPSHOT'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.realm:realm-android:0.84.2'
    compile 'com.jakewharton.timber:timber:2.5.0'
    compile 'info.metadude.android:typed-preferences:2.0.0'
    compile 'com.google.android.gms:play-services-maps:8.3.0'
    compile 'com.google.android.gms:play-services-location:8.3.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.3'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.bugsnag:bugsnag-android:3.2.2'
    compile 'commons-lang:commons-lang:2.6'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    compile 'pl.charmas.android:android-reactive-location:0.8@aar'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile ('com.guillaumedelente.android.contacts:contact-picker:1@aar') {
        transitive=true
    }
}

allprojects {
    afterEvaluate { project ->
        def propsFile = file('keystore.properties')
        def configName = 'release'

        if (android.signingConfigs.hasProperty(configName)) {
            if (!propsFile.exists()) {
                throw new RuntimeException("keystore.properties must exist to sign a release build");
            }
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}